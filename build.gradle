buildscript {
    repositories {
        maven { url 'http://artifactory.uk1.prod.skyscanner.local:8081/artifactory/repo' }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

apply plugin: 'com.google.protobuf'

project.ext {
    grpcVersion = '1.5.0'
    mshellVersion = '6.4.3' //Must be >=6.3.2 for gRPC support
}

dependencies {
    compile "net.skyscanner.mshell:mshell-java-dropwizard-core:${mshellVersion}"
    testCompile "net.skyscanner.mshell:mshell-java-testing:${mshellVersion}"
}

compileJava.dependsOn{
    generateProto
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.4.0'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                // To generate deprecated interfaces and static bindService method,
                // turn the enable_deprecated option to true below:
                option 'enable_deprecated=false'
            }
        }
    }
}

// Add generated classes as a source for idea and packaging

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
        proto {
            srcDirs 'proto/'
            srcDirs 'vendor/proto/'
        }
    }
}

// Prevent sonarqube linting warnings for generated classes

sonarqube {
    properties {
        property "sonar.exclusions", "build/**/*"
    }
}